/* arkouda server
backend chapel program to mimic ndarray from numpy
This is the main driver for the arkouda server */

use Security;
use ServerConfig;
use Time only;
use ZMQ only;
use Memory;
use FileSystem;
use IO;
use Path;
use MultiTypeSymbolTable;
use MultiTypeSymEntry;
use MsgProcessing;
use GenSymIO;
use SymArrayDmap;
use ServerErrorStrings;


proc main() {
    writeln("arkouda server version = ",arkoudaVersion); try! stdout.flush();
    writeln("memory tracking = ", memTrack); try! stdout.flush();
    if (memTrack) {
        writeln("getMemLimit() = ",getMemLimit());
        writeln("bytes of memoryUsed() = ",memoryUsed());
        try! stdout.flush();
    }

    var st = new owned SymTab();
    var shutdownServer = false;
    var serverToken : string;
    var serverMessage : string;

    // create and connect ZMQ socket
    var context: ZMQ.Context;
    var socket : ZMQ.Socket = context.socket(ZMQ.REP);

    // configure token authentication and server startup message accordingly
    if authenticate {
        serverToken = getArkoudaToken('%s%s%s%s%s'.format(here.cwd(), pathSep, 
                                                             '.arkouda', pathSep, 'tokens.txt'));
        serverMessage = "server listening on %s:%t with token %s".format(serverHostname, 
                                        ServerPort, serverToken);
    } else {
        serverMessage = "server listening on %s:%t".format(serverHostname, ServerPort);
    }

    socket.bind("tcp://*:%t".format(ServerPort));

    writeln(serverMessage); try! stdout.flush();
    
    createServerConnectionInfo();

    var reqCount: int = 0;
    var repCount: int = 0;

    var t1 = new Time.Timer();
    t1.clear();
    t1.start();

    proc sendRepMsg(repMsg: ?t) where t==string || t==bytes {
        repCount += 1;
        if logging {
          if t==bytes {
              writeln("repMsg:"," <binary-data>");
          } else {
            writeln("repMsg:",repMsg);
          }
          try! stdout.flush();
        }
        socket.send(repMsg);
    }

    proc authenticateUser(token : string) throws {
        if token == 'None' || token.isEmpty() {
            throw new owned ErrorWithMsg("Error: access to arkouda requires token");
        }
        else if serverToken != token {
            throw new owned ErrorWithMsg("Error: token %s does not match server token, check with server owner".format(token));
        }
    } 

    proc containsBinaryData(cmdRaw : bytes) : bool {
        return cmdRaw.endsWith(b":array");
    }
    
    proc getCommandStrings(rawCmdString : string) : (string,string,string) {
        var strings = rawCmdString.split(':');
        return (strings[0],strings[1],strings[2]);
    }

    proc shutdown() {
        shutdownServer = true;
        repCount += 1;
        socket.send("shutdown server (%i req)".format(repCount));
    }
    
    while !shutdownServer {
        // receive message on the zmq socket
        var reqMsgRaw = socket.recv(bytes);

        reqCount += 1;

        var s0 = t1.elapsed();
        
        /*
        Separate the first tuple, which is a string binary 
        containing the message's cmdd, user, and token from
        the remaining payload. Depending upon the message type 
        (string or binary) the payload is either a space-delimited
        string or bytes
        */
        const (cmdRaw, payload) = reqMsgRaw.splitMsgToTuple(2);
        var user, token, cmd: string;

        // parse requests, execute requests, format responses
        try {
            /*
            decode the infrastructure string binary containing the user, 
            token, and cmd. If there is an error, discontinue processing 
            message and send an error message back to the client.
            */
            var cmdStr : string;
            try! {
                 cmdStr = cmdRaw.decode();
            } catch e: DecodeError {
               if v {
                    writeln("Error: illegal byte sequence in command: ",
                            cmdRaw.decode(decodePolicy.replace));
                    try! stdout.flush();
               }
               sendRepMsg(unknownError(""));
            }

            //parse the infrastruture string to retrieve user,token,cmd
            var (user,token,cmd) = getCommandStrings(cmdStr);

            /*
            If authentication is enabled with --authenticate flag, authenticate
            the user which for now consists of matching the submitted token
            with the token generated by the arkouda server
            */ 
            if authenticate {
                authenticateUser(token);
            }

            if containsBinaryData(cmdRaw) {
                /*
                For cases where arbitrary data is received, reconstitute the 
                reqMsgRaw binary to contain just the encoded cmd and the payload 
                and then send back to the client.
                */
                reqMsgRaw = b' '.join(cmd.encode(),payload); 
                if logging {
                    writeln("reqMsg: ", b"array", " <binary-data>");
                    writeln(">>> %s started at %.17r sec".format("array", s0));
                    try! stdout.flush();
                }
                sendRepMsg(arrayMsg(cmd, payload, st));
                //sendRepMsg(arrayMsg(reqMsgRaw, st));
            } else {
                /*
                The remaining payload is a space-delimited string binary, so decode
                and split the payload, and then reconstitute the reqMsg to contain
                the cmd and payload values
                */
                var messageTokens = payload.decode().split(' ');
                var reqMsg = ' '.join(messageTokens);
                reqMsg = ' '.join(cmd, reqMsg);

                if logging {
                    writeln("reqMsg: ", reqMsg);
                    writeln(">>> %s started at %.17r sec".format(cmd, s0));
                    try! stdout.flush();
                }

                // now take care of the case where we send arbitrary data:
                if cmd == "tondarray" {
                    sendRepMsg(tondarrayMsg(cmd, payload, st));
                }
                else {
                    // here we know that everything is strings
                    var repMsg: string;

                    if cmd == "shutdown" {
                        shutdown();
                        if (logging) {writeln("<<< shutdown took %.17r sec".format(t1.elapsed() - s0)); 
                                                                              try! stdout.flush();}
                        break;
                    }

                    select cmd
                    {
                        //when "array"             {repMsg = arrayMsg(cmd, payload, st);}
                        //when "tondarray"         {repMsg = tondarrayMsg(cmd, payload,st);}	 
                        when "intersect1d"       {repMsg = intersect1dMsg(cmd, payload, st);}
                        when "setdiff1d"         {repMsg = setdiff1dMsg(cmd, payload, st);}
                        when "setxor1d"          {repMsg = setxor1dMsg(cmd, payload, st);}
                        when "union1d"           {repMsg = union1dMsg(cmd, payload, st);}
                        when "segmentLengths"    {repMsg = segmentLengthsMsg(cmd, payload, st);}
                        when "segmentedHash"     {repMsg = segmentedHashMsg(cmd, payload, st);}
                        when "segmentedEfunc"    {repMsg = segmentedEfuncMsg(cmd, payload, st);}
                        when "segmentedIndex"    {repMsg = segmentedIndexMsg(cmd, payload, st);}
                        when "segmentedBinopvv"  {repMsg = segBinopvvMsg(cmd, payload, st);}
                        when "segmentedBinopvs"  {repMsg = segBinopvsMsg(cmd, payload, st);}
                        when "segmentedGroup"    {repMsg = segGroupMsg(cmd, payload, st);}
                        when "segmentedIn1d"     {repMsg = segIn1dMsg(cmd, payload, st);}
                        when "lshdf"             {repMsg = lshdfMsg(cmd, payload, st);}
                        when "readhdf"           {repMsg = readhdfMsg(cmd, payload, st);}
                        when "readAllHdf"        {repMsg = readAllHdfMsg(cmd, payload, st);}
                        when "tohdf"             {repMsg = tohdfMsg(cmd, payload, st);}
                        when "create"            {repMsg = createMsg(cmd, payload, st);}
                        when "delete"            {repMsg = deleteMsg(cmd, payload, st);}
                        when "binopvv"           {repMsg = binopvvMsg(cmd, payload, st);}
                        when "binopvs"           {repMsg = binopvsMsg(cmd, payload, st);}
                        when "binopsv"           {repMsg = binopsvMsg(cmd, payload, st);}
                        when "opeqvv"            {repMsg = opeqvvMsg(cmd, payload, st);}
                        when "opeqvs"            {repMsg = opeqvsMsg(cmd, payload, st);}
                        when "efunc"             {repMsg = efuncMsg(cmd, payload, st);}
                        when "efunc3vv"          {repMsg = efunc3vvMsg(cmd, payload, st);}
                        when "efunc3vs"          {repMsg = efunc3vsMsg(cmd, payload, st);}
                        when "efunc3sv"          {repMsg = efunc3svMsg(cmd, payload, st);}
                        when "efunc3ss"          {repMsg = efunc3ssMsg(cmd, payload, st);}
                        when "reduction"         {repMsg = reductionMsg(cmd, payload, st);}
                        when "countReduction"    {repMsg = countReductionMsg(cmd, payload, st);}
                        when "countLocalRdx"     {repMsg = countLocalRdxMsg(cmd, payload, st);}
                        when "findSegments"      {repMsg = findSegmentsMsg(cmd, payload, st);}
                        when "findLocalSegments" {repMsg = findLocalSegmentsMsg(cmd, payload, st);}
                        when "segmentedReduction"{repMsg = segmentedReductionMsg(cmd, payload, st);}
                        when "segmentedLocalRdx" {repMsg = segmentedLocalRdxMsg(cmd, payload, st);}
                        when "arange"            {repMsg = arangeMsg(cmd, payload, st);}
                        when "linspace"          {repMsg = linspaceMsg(cmd, payload, st);}
                        when "randint"           {repMsg = randintMsg(cmd, payload, st);}
                        when "randomNormal"      {repMsg = randomNormalMsg(cmd, payload, st);}
                        when "randomStrings"     {repMsg = randomStringsMsg(cmd, payload, st);}
                        when "histogram"         {repMsg = histogramMsg(cmd, payload, st);}
                        when "in1d"              {repMsg = in1dMsg(cmd, payload, st);}
                        when "unique"            {repMsg = uniqueMsg(cmd, payload, st);}
                        when "value_counts"      {repMsg = value_countsMsg(cmd, payload, st);}
                        when "set"               {repMsg = setMsg(cmd, payload, st);}
                        when "info"              {repMsg = infoMsg(cmd, payload, st);}
                        when "str"               {repMsg = strMsg(cmd, payload, st);}
                        when "repr"              {repMsg = reprMsg(cmd, payload, st);}
                        when "[int]"             {repMsg = intIndexMsg(cmd, payload, st);}
                        when "[slice]"           {repMsg = sliceIndexMsg(cmd, payload, st);}
                        when "[pdarray]"         {repMsg = pdarrayIndexMsg(cmd, payload, st);}
                        when "[int]=val"         {repMsg = setIntIndexToValueMsg(cmd, payload, st);}
                        when "[pdarray]=val"     {repMsg = setPdarrayIndexToValueMsg(cmd, payload, st);}
                        when "[pdarray]=pdarray" {repMsg = setPdarrayIndexToPdarrayMsg(cmd, payload, st);}
                        when "[slice]=val"       {repMsg = setSliceIndexToValueMsg(cmd, payload, st);}
                        when "[slice]=pdarray"   {repMsg = setSliceIndexToPdarrayMsg(cmd, payload, st);}
                        when "argsort"           {repMsg = argsortMsg(cmd, payload, st);}
                        when "coargsort"         {repMsg = coargsortMsg(cmd, payload, st);}
                        when "concatenate"       {repMsg = concatenateMsg(cmd, payload, st);}
                        when "localArgsort"      {repMsg = localArgsortMsg(cmd, payload, st);}
                        when "sort"              {repMsg = sortMsg(cmd, payload, st);}
                        when "joinEqWithDT"      {repMsg = joinEqWithDTMsg(cmd, payload, st);}
                        when "getconfig"         {repMsg = getconfigMsg(cmd, payload, st);}
                        when "getmemused"        {repMsg = getmemusedMsg(cmd, payload, st);}
                        when "register"          {repMsg = registerMsg(cmd, payload, st);}
                        when "attach"            {repMsg = attachMsg(cmd, payload, st);}
                        when "unregister"        {repMsg = unregisterMsg(cmd, payload, st);}
                        when "connect" {
                            if authenticate {
                                repMsg = "connected to arkouda server tcp://*:%t as user %s with token %s".format(ServerPort,user,token);
                            } else {
                                repMsg = "connected to arkouda server tcp://*:%t".format(ServerPort);
                            }
                        }
                        when "disconnect" {
                            repMsg = "disconnected from arkouda server tcp://*:%t".format(ServerPort);
                        }
                        when "noop" {
                            repMsg = "noop";
                            if v { writeln("no-op"); try! stdout.flush(); }
                        }
                        when "ruok" {
                            repMsg = "imok";
                        }
                        otherwise {
                            repMsg = "Error: unrecognized command: %s".format(reqMsg);
                        }

                    }
                    sendRepMsg(repMsg);
                }
            }

            // log the fact the request message has been handled and reply message has been sent
            if (logging) {writeln("<<< %s took %.17r sec".format(cmd, t1.elapsed() - s0)); try! stdout.flush();}
            if (logging && memTrack) {writeln("bytes of memory used after command = ", 
                                                               memoryUsed():uint * numLocales:uint); try! stdout.flush();}
        } catch (e: ErrorWithMsg) {
            sendRepMsg(e.msg);
            if (logging) {writeln("<<< %s resulted in error %s in  %.17r sec".format(cmdRaw.decode(decodePolicy.replace), 
                                                                              e.msg, t1.elapsed() - s0)); try! stdout.flush();}
        } catch {
            sendRepMsg(unknownError(""));
            if (logging) {writeln("<<< %s resulted in unknownError in %.17r sec".format(cmdRaw.decode(decodePolicy.replace), 
                                                                                     t1.elapsed() - s0)); try! stdout.flush();}
        }
        
    }
    t1.stop();
    deleteServerConnectionInfo();

    writeln("requests = ",reqCount," responseCount = ",repCount," elapsed sec = ",t1.elapsed());
}

proc createServerConnectionInfo() {
    use IO;
    if !serverConnectionInfo.isEmpty() {
        try! {
            var w = open(serverConnectionInfo, iomode.cw).writer();
            w.writef("%s %t\n", serverHostname, ServerPort);
        }
    }
}

proc deleteServerConnectionInfo() {
    use FileSystem;
    if !serverConnectionInfo.isEmpty() {
        try! {
            remove(serverConnectionInfo);
        }
    }
}
